/**
 * Copyright 2019 California Institute of Technology ("Caltech").
 * U.S. Government sponsorship acknowledged.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar io.opencaesar.oml.dsl.Oml hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://opencaesar.io/oml" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Ontology:
	VocabularyBox |
	DescriptionBox;

Annotation:
	'@' {Annotation} property=[AnnotationProperty|Ref] (literalValue=Literal | referenceValue=[Member|Ref])?;

VocabularyBox:
	Vocabulary |
	VocabularyBundle;

Vocabulary:
	ownedAnnotations+=Annotation*
	'vocabulary' namespace=NAMESPACE 'as' prefix=ID  '{'
		ownedImports+=(Extension|Usage)*
		ownedStatements+=VocabularyStatement*
	'}';

VocabularyBundle:
	ownedAnnotations+=Annotation*
	'vocabulary' 'bundle' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=(Extension|Inclusion)*
	'}';

DescriptionBox:
	Description |
	DescriptionBundle;
	
Description:
	ownedAnnotations+=Annotation*
	'description' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=(Extension|Usage)*
		ownedStatements+=DescriptionStatement*
	'}';

DescriptionBundle:
	ownedAnnotations+=Annotation*
	'description' 'bundle' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=(Extension|Usage|Inclusion)*
	'}';

SpecializableTerm:
	Type |
	AnnotationProperty |
	ScalarProperty |
	StructuredProperty |
	UnreifiedRelation;

Type:
	Classifier |
	Scalar;	

Classifier:
	Entity |
	Structure;
	
Entity:
	Aspect |
	Concept |
	RelationEntity;

Aspect:
	ownedAnnotations+=Annotation*
	('aspect' name=ID | 'ref' 'aspect' ref=[Aspect|Ref]) ('['
		(ownedKeys+=KeyAxiom)* 
	']')? (ClassifierSpecialization)? (ClassifierEquivalence)?;

Concept:
	ownedAnnotations+=Annotation*
	('concept' name=ID | 'ref' 'concept' ref=[Concept|Ref]) ('['
		(ownedEnumeration=InstanceEnumerationAxiom)?
		(ownedKeys+=KeyAxiom)* 
	']')? (ClassifierSpecialization)? (ClassifierEquivalence)?;
	
RelationEntity:
	ownedAnnotations+=Annotation*
	('relation' 'entity' name=ID | 'ref' 'relation' 'entity' ref=[RelationEntity|Ref]) ('['
		('from' sources+=[Entity|Ref] (',' sources+=[Entity|Ref])*)?
		('to' targets+=[Entity|Ref] (',' targets+=[Entity|Ref])*)?
		(forwardRelation=ForwardRelation)?
		(reverseRelation=ReverseRelation)?
		((functional?='functional')? &
		 (inverseFunctional?='inverse' 'functional')? &
		 (symmetric?='symmetric')? &
		 (asymmetric?='asymmetric')? &
		 (reflexive?='reflexive')? &
		 (irreflexive?='irreflexive')? &
		 (transitive?='transitive')?)
		(ownedKeys+=KeyAxiom)*
	']')? (ClassifierSpecialization)? (ClassifierEquivalence)?;
	
Structure:
	ownedAnnotations+=Annotation*
	('structure' name=ID | 'ref' 'structure' ref=[Structure|Ref])
		(ClassifierSpecialization)? (ClassifierEquivalence)?;

fragment ClassifierSpecialization returns Classifier:
	'<' (ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)* 
		|
		(ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? '['
			ownedPropertyRestrictions+=PropertyRestrictionAxiom*
		']');

fragment ClassifierEquivalence returns Classifier:
	'=' ownedEquivalences+=ClassifierEquivalenceAxiom (',' ownedEquivalences+=ClassifierEquivalenceAxiom)*;

ClassifierEquivalenceAxiom:
	superClassifiers+=[Classifier|Ref] ('&' superClassifiers+=[Classifier|Ref])*
	|
	{ClassifierEquivalenceAxiom} 
	(superClassifiers+=[Classifier|Ref] ('&' superClassifiers+=[Classifier|Ref])*)? '['
		ownedPropertyRestrictions+=PropertyRestrictionAxiom*
	']';

Scalar:
	ownedAnnotations+=Annotation*
	('scalar' name=ID | 'ref' 'scalar' ref=[Scalar|Ref]) ('['
		(('length' length=UnsignedInteger)? &
		 ('minLength' minLength=UnsignedInteger)? &
		 ('maxLength' maxLength=UnsignedInteger)? &
		 ('pattern' pattern=STRING)? &
		 ('language' language=ID)? &
		 ('minInclusive' minInclusive=Literal)? &
		 ('minExclusive' minExclusive=Literal)? &
		 ('maxInclusive' maxInclusive=Literal)? &
		 ('maxExclusive' maxExclusive=Literal)? &
		 (ownedEnumeration=LiteralEnumerationAxiom)?)
	']')? (ScalarSpecialization)?;

fragment ScalarSpecialization returns Scalar:
	'<' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*;

Property:
	AnnotationProperty |
	SemanticProperty;

AnnotationProperty:
	ownedAnnotations+=Annotation*
	('annotation' 'property' name=ID | 'ref' 'annotation' 'property' ref=[AnnotationProperty|Ref])
		(PropertySpecialization)? (PropertyEquivalence)?;

SemanticProperty:
	ScalarProperty |
	StructuredProperty |
	Relation;

ScalarProperty:
	ownedAnnotations+=Annotation*
	('scalar' 'property' name=ID | 'ref' 'scalar' 'property' ref=[ScalarProperty|Ref]) ('['
		('domain' domains+=[Classifier|Ref] (',' domains+=[Classifier|Ref])*)?
		('range' ranges+=[Scalar|Ref] (',' ranges+=[Scalar|Ref])*)?
		(functional?='functional')?
	']')? (PropertySpecialization)? (PropertyEquivalence)?;

StructuredProperty:
	ownedAnnotations+=Annotation*
	('structured' 'property' name=ID | 'ref' 'structured' 'property' ref=[StructuredProperty|Ref]) ('['
		('domain' domains+=[Classifier|Ref] (',' domains+=[Classifier|Ref])*)?
		('range' ranges+=[Structure|Ref] (',' ranges+=[Structure|Ref])*)?
		(functional?='functional')?
	']')? (PropertySpecialization)? (PropertyEquivalence)?;

Relation:
	ForwardRelation |
	ReverseRelation |
	UnreifiedRelation;

ForwardRelation:
	ownedAnnotations+=Annotation*
	'forward' name=ID;

ReverseRelation:
	ownedAnnotations+=Annotation*
	'reverse' name=ID;

UnreifiedRelation:
	ownedAnnotations+=Annotation*
	('relation' name=ID | 'ref' 'relation' ref=[Relation|Ref]) ('['
		('from' sources+=[Entity|Ref] (',' sources+=[Entity|Ref])*)?
		('to' targets+=[Entity|Ref] (',' targets+=[Entity|Ref])*)?
		(reverseRelation=ReverseRelation)?
		((functional?='functional')? &
		 (inverseFunctional?='inverse' 'functional')? &
		 (symmetric?='symmetric')? &
		 (asymmetric?='asymmetric')? &
		 (reflexive?='reflexive')? &
		 (irreflexive?='irreflexive')? &
		 (transitive?='transitive')?)
	']')? (PropertySpecialization)? (PropertyEquivalence)?;

fragment PropertySpecialization returns SpecializableProperty:
	'<' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*;

fragment PropertyEquivalence returns SpecializableProperty:
	'=' ownedEquivalences+=PropertyEquivalenceAxiom (',' ownedEquivalences+=PropertyEquivalenceAxiom)*;

Rule:
	ownedAnnotations+=Annotation*
	('rule' name=ID | 'ref' 'rule' ref=[Rule|Ref]) ('['
		(antecedent+=Predicate ('^' antecedent+=Predicate)* '->' consequent+=Predicate ('^' consequent+=Predicate)*)?
	']')?;

BuiltIn:
	ownedAnnotations+=Annotation*
	('builtin' name=ID | 'ref' 'builtin' ref=[BuiltIn|Ref]);

StructureInstance:
	
	type=[Structure|Ref] '['
		ownedPropertyValues+=PropertyValueAssertion*
	']';
	
NamedInstance:
	ConceptInstance |
	RelationInstance;

ConceptInstance:
	ownedAnnotations+=Annotation*
	('instance' name=ID | 'ref' 'instance' ref=[ConceptInstance|Ref]) (':' ownedTypes+=ConceptTypeAssertion (',' ownedTypes+=ConceptTypeAssertion)*)? ('['
		ownedPropertyValues+=PropertyValueAssertion*
	']')?;
	
RelationInstance:
	ownedAnnotations+=Annotation*
	('relation' 'instance' name=ID | 'ref' 'relation' 'instance' ref=[RelationInstance|Ref]) (':' ownedTypes+=RelationTypeAssertion (',' ownedTypes+=RelationTypeAssertion)*)? ('['
		('from' sources+=[NamedInstance|Ref] (',' sources+=[NamedInstance|Ref])*)? 
		('to' targets+=[NamedInstance|Ref] (',' targets+=[NamedInstance|Ref])*)?
		ownedPropertyValues+=PropertyValueAssertion*
	']')?;
	
VocabularyStatement:
	Rule |
	BuiltIn |
	SpecializableTerm;
	
DescriptionStatement:
	NamedInstance;
	
Import:
	Extension |
	Usage |
	Inclusion;

Extension returns Import:
	kind=Extends namespace=NAMESPACE ('as' prefix=ID)?;
    	
Usage returns Import:
	kind=Uses namespace=NAMESPACE ('as' prefix=ID)?;
    	
Inclusion returns Import:
	kind=Includes namespace=NAMESPACE ('as' prefix=ID)?;

SpecializationAxiom:
	superTerm=[SpecializableTerm|Ref];

PropertyEquivalenceAxiom:
	superProperty=[Property|Ref];

PropertyRestrictionAxiom:
	PropertyRangeRestrictionAxiom |
	PropertyCardinalityRestrictionAxiom |
	PropertyValueRestrictionAxiom;

PropertyRangeRestrictionAxiom:
	ScalarPropertyRangeRestrictionAxiom |
	StructuredPropertyRangeRestrictionAxiom |
	RelationRangeRestrictionAxiom;

ScalarPropertyRangeRestrictionAxiom returns PropertyRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'scalar' 'property' property=[ScalarProperty|Ref] 'to' range=[Scalar|Ref];

StructuredPropertyRangeRestrictionAxiom returns PropertyRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'structured' 'property' property=[StructuredProperty|Ref] 'to' range=[Structure|Ref];

RelationRangeRestrictionAxiom returns PropertyRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'relation' property=[Relation|Ref] 'to' range=[Entity|Ref];

PropertyCardinalityRestrictionAxiom:
	ScalarPropertyCardinalityRestrictionAxiom |
	StructuredPropertyCardinalityRestrictionAxiom |
	RelationCardinalityRestrictionAxiom;

ScalarPropertyCardinalityRestrictionAxiom returns PropertyCardinalityRestrictionAxiom:
	'restricts' 'scalar' 'property' property=[ScalarProperty|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Scalar|Ref])?;

StructuredPropertyCardinalityRestrictionAxiom returns PropertyCardinalityRestrictionAxiom:
	'restricts' 'structured' 'property' property=[StructuredProperty|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Structure|Ref])?;

RelationCardinalityRestrictionAxiom returns PropertyCardinalityRestrictionAxiom:
	'restricts' 'relation' property=[Relation|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Entity|Ref])?;

PropertyValueRestrictionAxiom:
	ScalarPropertyValueRestrictionAxiom |
	StructuredPropertyValueRestrictionAxiom |
	RelationValueRestrictionAxiom;
	
ScalarPropertyValueRestrictionAxiom returns PropertyValueRestrictionAxiom:
	'restricts' 'scalar' 'property' property=[ScalarProperty|Ref] 'to' literalValue=Literal;

StructuredPropertyValueRestrictionAxiom returns PropertyValueRestrictionAxiom:
	'restricts' 'structured' 'property' property=[StructuredProperty|Ref] 'to' structureInstanceValue=StructureInstance;

RelationValueRestrictionAxiom returns PropertyValueRestrictionAxiom:
	'restricts' 'relation' property=[Relation|Ref] 'to' namedInstanceValue=[NamedInstance|Ref];

KeyAxiom:
	'key' properties+=[Property|Ref] (',' properties+=[Property|Ref])*;

InstanceEnumerationAxiom:
	'oneOf' instances+=[ConceptInstance|Ref] (',' instances+=[ConceptInstance|Ref])*;

LiteralEnumerationAxiom:
	'oneOf' literals+=Literal (',' literals+=Literal)*;

ConceptTypeAssertion returns TypeAssertion:
	type=[Concept|Ref];

RelationTypeAssertion returns TypeAssertion:
	type=[RelationEntity|Ref];

PropertyValueAssertion:
	ScalarPropertyValueAssertion |
	StructuredPropertyValueAssertion |
	LinkAssertion;

ScalarPropertyValueAssertion returns PropertyValueAssertion:
	property=[ScalarProperty|Ref] literalValue=Literal;

StructuredPropertyValueAssertion returns PropertyValueAssertion:
	property=[StructuredProperty|Ref] structureInstanceValue=StructureInstance;

LinkAssertion returns PropertyValueAssertion:
	property=[Relation|Ref] namedInstanceValue=[NamedInstance|Ref];

Predicate:
	UnaryPredicate |
	BinaryPredicate |
	BuiltInPredicate;

UnaryPredicate:
	TypePredicate |
	RelationEntityPredicate;
	
BinaryPredicate:
    PropertyPredicate |
    SameAsPredicate |
    DifferentFromPredicate;

TypePredicate:
	type=[Type|Ref] '(' argument=Argument ')';
	
RelationEntityPredicate:
    type=[RelationEntity|Ref] '(' argument1=Argument ',' argument=Argument ',' argument2=Argument ')';

PropertyPredicate:
	property=[Property|Ref] '(' argument1=Argument ',' argument2=Argument ')';

SameAsPredicate:
    'sameAs' '(' argument1=Argument ',' argument2=Argument ')';

DifferentFromPredicate:
    'differentFrom' '(' argument1=Argument ',' argument2=Argument ')';

BuiltInPredicate:
    'builtIn' '(' builtIn=[BuiltIn|Ref] ',' arguments+=Argument (',' arguments+=Argument)* ')';

Argument:
	variable=ID | literal=Literal | instance=[NamedInstance|CrossRef]
;

Literal:
	IntegerLiteral |
	DecimalLiteral |
	DoubleLiteral |
	BooleanLiteral |
	QuotedLiteral;

IntegerLiteral:
	value=Integer;

DecimalLiteral:
	value=Decimal;

DoubleLiteral:
	value=Double;

BooleanLiteral:
	value=Boolean;

QuotedLiteral:
	value=STRING (('^^' type=[Scalar|Ref]) | ('$' langTag=ID))?;

enum RangeRestrictionKind:
	all |
	some;

enum CardinalityRestrictionKind:
	exactly |
	min |
	max;

enum Extends returns ImportKind:
	extension = 'extends';

enum Uses returns ImportKind:
	usage = 'uses';

enum Includes returns ImportKind:
	inclusion = 'includes';

//--------------

Ref returns ecore::EString: 							ID | QNAME | IRI;

CrossRef returns ecore::EString: 						QNAME | IRI;

Boolean returns ecore::EBoolean:						BOOLEAN_STR;

UnsignedInteger returns UnsignedInt: 					UNSIGNED_INTEGER_STR;

Integer returns ecore::EInt: 							UNSIGNED_INTEGER_STR | INTEGER_STR;

Decimal returns Decimal:								DECIMAL_STR;

Double returns ecore::EDouble:							DOUBLE_STR;

//----------------------------

terminal BOOLEAN_STR:									'false'|'true';

terminal UNSIGNED_INTEGER_STR:							DIGIT+;

terminal INTEGER_STR:									('+'|'-')? DIGIT+;

terminal DECIMAL_STR:									('+'|'-')? (DIGIT+('.'DIGIT*)? | ('.'DIGIT+));

terminal DOUBLE_STR:									('+'|'-')? (DIGIT+('.'DIGIT*)? | ('.'DIGIT+)) (('e'|'E') ('+'|'-')? DIGIT+)?;

terminal STRING:										('"' (!('"'|'\\') | '\\'.)* '"') | ("'" (!("'"|'\\') | '\\'.)* "'") | ("'''" -> "'''") | '"""' -> '"""';

terminal NAMESPACE:										'<' (!('>'|' '|'#'))* ('#'|'/') '>';

terminal IRI:											'<' (!('>'|' '))* '>';

terminal ID:											IDFRAG;

terminal QNAME:											IDFRAG ':' IDFRAG;

terminal ML_COMMENT: 									'/*' -> '*/';
 
terminal SL_COMMENT:									'//' !('\n'|'\r')* ('\r'? '\n')?;
 
terminal WS: 											(' '|'\t'|'\r'|'\n')+;

//----------------------------

terminal fragment IDFRAG:								'^'? (ALPHA|DIGIT|'_') (ALPHA|DIGIT|'_'|'-'|'.'|'$')*;

terminal fragment ALPHA:								'a'..'z'| 'A'..'Z';

terminal fragment DIGIT: 								'0'..'9';