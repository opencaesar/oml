/**
 * 
 * Copyright 2019 California Institute of Technology ("Caltech").
 * U.S. Government sponsorship acknowledged.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * 
 */
grammar io.opencaesar.oml.dsl.Oml hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://opencaesar.io/oml" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Ontology:
	VocabularyBox |
	DescriptionBox;

Annotation:
	'@' {Annotation} property=[AnnotationProperty|Ref] (value=Literal | referenceValue=[Member|Ref])?;

VocabularyBox:
	Vocabulary |
	VocabularyBundle;

Vocabulary:
	ownedAnnotations+=Annotation*
	'vocabulary' namespace=NAMESPACE 'as' prefix=ID  '{'
		ownedImports+=VocabularyImport*
		ownedStatements+=VocabularyStatement*
	'}';

VocabularyBundle:
	ownedAnnotations+=Annotation*
	'vocabulary' 'bundle' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=VocabularyBundleImport*
	'}';

DescriptionBox:
	Description |
	DescriptionBundle;
	
Description:
	ownedAnnotations+=Annotation*
	'description' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=DescriptionImport*
		ownedStatements+=DescriptionStatement*
	'}';

DescriptionBundle:
	ownedAnnotations+=Annotation*
	'description' 'bundle' namespace=NAMESPACE 'as' prefix=ID '{'
		ownedImports+=DescriptionBundleImport*
	'}';

SpecializableTerm:
	Type |
	AnnotationProperty |
	ScalarProperty |
	StructuredProperty;
	
Type:
	Classifier |
	Scalar;	

Classifier:
	Entity |
	Structure;
	
Entity:
	Aspect |
	Concept |
	RelationEntity;

Aspect:
	ownedAnnotations+=Annotation*
	'aspect' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(ownedKeys+=KeyAxiom | 
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
	']')?;

Concept:
	ownedAnnotations+=Annotation*
	'concept' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(ownedKeys+=KeyAxiom | 
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
		('enumerates' enumeratedInstances+=[ConceptInstance|Ref] (',' enumeratedInstances+=[ConceptInstance|Ref])*)?
	']')?;
	
RelationEntity:
	ownedAnnotations+=Annotation*
	'relation' 'entity' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? '['
		'from' source=[Entity|Ref]
		'to' target=[Entity|Ref]
		((forwardRelation=ForwardRelation)? &
		 (reverseRelation=ReverseRelation)?)
		((functional?='functional')? &
		 (inverseFunctional?='inverse' 'functional')? &
		 (symmetric?='symmetric')? &
		 (asymmetric?='asymmetric')? &
		 (reflexive?='reflexive')? &
		 (irreflexive?='irreflexive')? &
		 (transitive?='transitive')?)
		(ownedKeys+=KeyAxiom |
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
	']';
	
Structure:
	ownedAnnotations+=Annotation*
	'structure' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		ownedPropertyRestrictions+=PropertyRestrictionAxiom*
	']')?;

Feature:
	Property;

Property:
	AnnotationProperty |
	SemanticProperty;

AnnotationProperty:
	ownedAnnotations+=Annotation*
	'annotation' 'property' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

SemanticProperty:
	ScalarProperty |
	StructuredProperty;

ScalarProperty:
	ownedAnnotations+=Annotation*
	'scalar' 'property' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		'domain' domain=[Classifier|Ref]
		'range' range=[Scalar|Ref]
		(functional?='functional')?
	']')?;

StructuredProperty:
	ownedAnnotations+=Annotation*
	'structured' 'property' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		'domain' domain=[Classifier|Ref]
		'range' range=[Structure|Ref]
		(functional?='functional')?
	']')?;

Scalar:
	FacetedScalar |
	EnumeratedScalar;

FacetedScalar:
	ownedAnnotations+=Annotation*
	'scalar' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		 (('length' length=UnsignedInteger)? &
		 ('minLength' minLength=UnsignedInteger)? &
		 ('maxLength' maxLength=UnsignedInteger)? &
		 ('pattern' pattern=STRING)? &
		 ('language' language=ID)? &
		 ('minInclusive' minInclusive=Literal)? &
		 ('minExclusive' minExclusive=Literal)? &
		 ('maxInclusive' maxInclusive=Literal)? &
		 ('maxExclusive' maxExclusive=Literal)?)
	']')?;

EnumeratedScalar:
	ownedAnnotations+=Annotation*
	'enumerated' 'scalar' name=ID (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(literals+=Literal (',' literals+=Literal)*)?
	']')?;

Relation:
	ForwardRelation |
	ReverseRelation;

ForwardRelation:
	ownedAnnotations+=Annotation*
	'forward' name=ID;

ReverseRelation:
	ownedAnnotations+=Annotation*
	'reverse' name=ID;

Rule:
	ownedAnnotations+=Annotation*
	'rule' name=ID '['
		antecedent+=Predicate ('^' antecedent+=Predicate)* '->' consequent+=Predicate ('^' consequent+=Predicate)*
	']';

StructureInstance:
	
	type=[Structure|Ref] '['
		ownedPropertyValues+=PropertyValueAssertion*
	']';
	
NamedInstance:
	ConceptInstance |
	RelationInstance;

ConceptInstance:
	ownedAnnotations+=Annotation*
	'ci' name=ID (':' ownedTypes+=ConceptTypeAssertion (',' ownedTypes+=ConceptTypeAssertion)*)? ('['
		(ownedPropertyValues+=PropertyValueAssertion | 
		 ownedLinks+=LinkAssertion)*
	']')?;
	
RelationInstance:
	ownedAnnotations+=Annotation*
	'ri' name=ID (':' ownedTypes+=RelationTypeAssertion (',' ownedTypes+=RelationTypeAssertion)*)? '['
		'from' sources+=[NamedInstance|Ref] (',' sources+=[NamedInstance|Ref])* 
		'to' targets+=[NamedInstance|Ref] (',' targets+=[NamedInstance|Ref])*
		(ownedPropertyValues+=PropertyValueAssertion |
		 ownedLinks+=LinkAssertion)*
	']';
	
SpecializableTermReference:
	ClassifierReference |
	FacetedScalarReference |
	EnumeratedScalarReference |
	AnnotationPropertyReference |
	ScalarPropertyReference |
	StructuredPropertyReference;

ClassifierReference:
	EntityReference |
	StructureReference;

EntityReference:
	AspectReference |
	ConceptReference |
	RelationEntityReference;
	
AspectReference:
	ownedAnnotations+=Annotation*
	'ref' 'aspect' aspect=[Aspect|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(ownedKeys+=KeyAxiom | 
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
	']')?;

ConceptReference:
	ownedAnnotations+=Annotation*
	'ref' 'concept' concept=[Concept|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(ownedKeys+=KeyAxiom | 
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
	']')?;

RelationEntityReference:
	ownedAnnotations+=Annotation*
	'ref' 'relation' 'entity' entity=[RelationEntity|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		(ownedKeys+=KeyAxiom | 
		 ownedPropertyRestrictions+=PropertyRestrictionAxiom |
		 ownedRelationRestrictions+=RelationRestrictionAxiom)*
	']')?;

StructureReference:
	ownedAnnotations+=Annotation*
	'ref' 'structure' structure=[Structure|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)? ('['
		ownedPropertyRestrictions+=PropertyRestrictionAxiom*
	']')?;

AnnotationPropertyReference:
	ownedAnnotations+=Annotation*
	'ref' 'annotation' 'property' property=[AnnotationProperty|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

ScalarPropertyReference:
	ownedAnnotations+=Annotation*
	'ref' 'scalar' 'property' property=[ScalarProperty|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

StructuredPropertyReference:
	ownedAnnotations+=Annotation*
	'ref' 'structured' 'property' property=[StructuredProperty|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

FacetedScalarReference:
	ownedAnnotations+=Annotation*
	'ref' 'scalar' scalar=[FacetedScalar|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

EnumeratedScalarReference:
	ownedAnnotations+=Annotation*
	'ref' 'enumerated' 'scalar' scalar=[EnumeratedScalar|Ref] (':>' ownedSpecializations+=SpecializationAxiom (',' ownedSpecializations+=SpecializationAxiom)*)?;

RelationReference:
	ownedAnnotations+=Annotation*
	'ref' 'relation' relation=[Relation|Ref];
	
RuleReference:
	ownedAnnotations+=Annotation*
	'ref' 'rule' rule=[Rule|Ref];

NamedInstanceReference:
	ConceptInstanceReference |
	RelationInstanceReference;

ConceptInstanceReference:
	ownedAnnotations+=Annotation*
	'ref' 'ci' instance=[ConceptInstance|Ref] (':' ownedTypes+=ConceptTypeAssertion (',' ownedTypes+=ConceptTypeAssertion)*)? ('['
		ownedPropertyValues+=PropertyValueAssertion*
		ownedLinks+=LinkAssertion*
	']')?;

RelationInstanceReference:
	ownedAnnotations+=Annotation*
	'ref' 'ri' instance=[RelationInstance|Ref] (':' ownedTypes+=RelationTypeAssertion (',' ownedTypes+=RelationTypeAssertion)*)? ('['
		ownedPropertyValues+=PropertyValueAssertion*
		ownedLinks+=LinkAssertion*
	']')?;

VocabularyStatement:
	Rule |
	RuleReference |
	SpecializableTerm |
	SpecializableTermReference |
	RelationReference;
	
DescriptionStatement:
	NamedInstance |
	NamedInstanceReference;
	
VocabularyImport:
	VocabularyExtension |
	VocabularyUsage;

VocabularyExtension:
	ownedAnnotations+=Annotation*
	'extends' namespace=NAMESPACE ('as' prefix=ID)?;
    	
VocabularyUsage:
	ownedAnnotations+=Annotation*
	'uses' namespace=NAMESPACE ('as' prefix=ID)?;
    	
VocabularyBundleImport:
	VocabularyBundleExtension |
	VocabularyBundleInclusion;

VocabularyBundleInclusion:
	ownedAnnotations+=Annotation*
	'includes' namespace=NAMESPACE ('as' prefix=ID)?;

VocabularyBundleExtension:
	ownedAnnotations+=Annotation*
	'extends' namespace=NAMESPACE ('as' prefix=ID)?;

DescriptionImport:
	DescriptionExtension |
	DescriptionUsage;

DescriptionUsage:
	ownedAnnotations+=Annotation*
	'uses' namespace=NAMESPACE ('as' prefix=ID)?;

DescriptionExtension:
	ownedAnnotations+=Annotation*
	'extends' namespace=NAMESPACE ('as' prefix=ID)?;

DescriptionBundleImport:
	DescriptionBundleExtension |
	DescriptionBundleInclusion |
	DescriptionBundleUsage;

DescriptionBundleInclusion:
	ownedAnnotations+=Annotation*
	'includes' namespace=NAMESPACE ('as' prefix=ID)?;

DescriptionBundleExtension:
	ownedAnnotations+=Annotation*
	'extends' namespace=NAMESPACE ('as' prefix=ID)?;

DescriptionBundleUsage:
	ownedAnnotations+=Annotation*
	'uses' namespace=NAMESPACE ('as' prefix=ID)?;

SpecializationAxiom:
	specializedTerm=[SpecializableTerm|Ref];
	
PropertyRestrictionAxiom:
	ScalarPropertyRestrictionAxiom |
	StructuredPropertyRestrictionAxiom;

ScalarPropertyRestrictionAxiom:
	ScalarPropertyRangeRestrictionAxiom |
	ScalarPropertyCardinalityRestrictionAxiom |
	ScalarPropertyValueRestrictionAxiom;

ScalarPropertyRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'scalar' 'property' property=[ScalarProperty|Ref] 'to' range=[Scalar|Ref];

ScalarPropertyCardinalityRestrictionAxiom:
	'restricts' 'scalar' 'property' property=[ScalarProperty|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Scalar|Ref])?;

ScalarPropertyValueRestrictionAxiom:
	'restricts' 'scalar' 'property' property=[ScalarProperty|Ref] 'to' value=Literal;

StructuredPropertyRestrictionAxiom:
	StructuredPropertyRangeRestrictionAxiom |
	StructuredPropertyCardinalityRestrictionAxiom |
	StructuredPropertyValueRestrictionAxiom;

StructuredPropertyRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'structured' 'property' property=[StructuredProperty|Ref] 'to' range=[Structure|Ref];

StructuredPropertyCardinalityRestrictionAxiom:
	'restricts' 'structured' 'property' property=[StructuredProperty|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Structure|Ref])?;

StructuredPropertyValueRestrictionAxiom:
	'restricts' 'structured' 'property' property=[StructuredProperty|Ref] 'to' value=StructureInstance;

RelationRestrictionAxiom:
	RelationRangeRestrictionAxiom |
	RelationCardinalityRestrictionAxiom |
	RelationTargetRestrictionAxiom;
	
RelationRangeRestrictionAxiom:
	'restricts' kind=RangeRestrictionKind 'relation' relation=[Relation|Ref] 'to' range=[Entity|Ref];

RelationCardinalityRestrictionAxiom:
	'restricts' 'relation' relation=[Relation|Ref] 'to' kind=CardinalityRestrictionKind cardinality=UnsignedInteger (range=[Entity|Ref])?;

RelationTargetRestrictionAxiom:
	'restricts' 'relation' relation=[Relation|Ref] 'to' target=[NamedInstance|Ref];

KeyAxiom:
	'key' properties+=[Feature|Ref] (',' properties+=[Feature|Ref])*;

ConceptTypeAssertion:
	type=[Concept|Ref];

RelationTypeAssertion:
	type=[RelationEntity|Ref];

PropertyValueAssertion:
	ScalarPropertyValueAssertion |
	StructuredPropertyValueAssertion;

ScalarPropertyValueAssertion:
	property=[ScalarProperty|Ref] value=Literal;

StructuredPropertyValueAssertion:
	property=[StructuredProperty|Ref] value=StructureInstance;

LinkAssertion:
	relation=[Relation|Ref] target=[NamedInstance|Ref];

Predicate:
	UnaryPredicate |
	BinaryPredicate;

UnaryPredicate:
	TypePredicate;
	
TypePredicate:
	type=[Type|Ref] => '(' variable=ID ')';
	
BinaryPredicate:
	RelationEntityPredicate |
    FeaturePredicate |
    SameAsPredicate |
    DifferentFromPredicate;

RelationEntityPredicate:
    entity=[RelationEntity|Ref] '(' variable1=ID ',' entityVariable=ID ',' (variable2=ID | instance2=[NamedInstance|CrossRef]) ')';

FeaturePredicate:
	feature=[Feature|Ref] '(' variable1=ID ',' (variable2=ID | literal2=Literal | instance2=[NamedInstance|CrossRef]) ')';

SameAsPredicate:
    'sameAs' '(' variable1=ID ',' (variable2=ID | instance2=[NamedInstance|CrossRef]) ')';

DifferentFromPredicate:
    'differentFrom' '(' variable1=ID ',' (variable2=ID | instance2=[NamedInstance|CrossRef]) ')';

Literal:
	IntegerLiteral |
	DecimalLiteral |
	DoubleLiteral |
	BooleanLiteral |
	QuotedLiteral;

IntegerLiteral:
	value=Integer;

DecimalLiteral:
	value=Decimal;

DoubleLiteral:
	value=Double;

BooleanLiteral:
	value=Boolean;

QuotedLiteral:
	value=STRING (('^^' type=[Scalar|Ref]) | ('$' langTag=ID))?;

enum RangeRestrictionKind:
	all |
	some;

enum CardinalityRestrictionKind:
	exactly |
	min |
	max;

//--------------

Ref returns ecore::EString: 							ID | QNAME | IRI;

CrossRef returns ecore::EString: 						QNAME | IRI;

Boolean returns ecore::EBoolean:						BOOLEAN_STR;

UnsignedInteger returns UnsignedInt: 					UNSIGNED_INTEGER_STR;

Integer returns ecore::EInt: 							UNSIGNED_INTEGER_STR | INTEGER_STR;

Decimal returns Decimal:								DECIMAL_STR;

Double returns ecore::EDouble:							DOUBLE_STR;

//----------------------------

terminal BOOLEAN_STR:									'false'|'true';

terminal UNSIGNED_INTEGER_STR:							DIGIT+;

terminal INTEGER_STR:									('+'|'-')? DIGIT+;

terminal DECIMAL_STR:									('+'|'-')? (DIGIT+('.'DIGIT*)? | ('.'DIGIT+));

terminal DOUBLE_STR:									('+'|'-')? (DIGIT+('.'DIGIT*)? | ('.'DIGIT+)) (('e'|'E') ('+'|'-')? DIGIT+)?;

terminal STRING:										('"' (!('"'|'\\') | '\\'.)* '"') | ("'" (!("'"|'\\') | '\\'.)* "'") | ("'''" -> "'''") | '"""' -> '"""';

terminal NAMESPACE:										'<' (!('>'|' '|'#'))* ('#'|'/') '>';

terminal IRI:											'<' (!('>'|' '))* '>';

terminal ID:											IDFRAG;

terminal QNAME:											IDFRAG ':' IDFRAG;

terminal ML_COMMENT: 									'/*' -> '*/';
 
terminal SL_COMMENT:									'//' !('\n'|'\r')* ('\r'? '\n')?;
 
terminal WS: 											(' '|'\t'|'\r'|'\n')+;

//----------------------------

terminal fragment IDFRAG:								'^'? (ALPHA|DIGIT|'_') (ALPHA|DIGIT|'_'|'-'|'.'|'$')*;

terminal fragment ALPHA:								'a'..'z'| 'A'..'Z';

terminal fragment DIGIT: 								'0'..'9';