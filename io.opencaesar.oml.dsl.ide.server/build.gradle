plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
}

ext.title = 'Oml Server IDE Support'
description = 'IDE support for Oml Server'

dependencies {
	api project(':io.opencaesar.oml.dsl')
	api "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
	api "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"

	api 'com.google.guava:guava:${guavaVersion}'
	api "org.eclipse.lsp4j:org.eclipse.lsp4j:${lsp4jVersion}"
	api "org.eclipse.elk:org.eclipse.elk.alg.layered:${elkVersion}"
	api "org.eclipse.elk:org.eclipse.elk.alg.graphviz.layouter:${elkVersion}"
	api "org.eclipse.sprotty:org.eclipse.sprotty.server:${sprottyVersion}"
	api "org.eclipse.sprotty:org.eclipse.sprotty.layout:${sprottyVersion}"
	api "org.eclipse.sprotty:org.eclipse.sprotty.xtext:${sprottyVersion}"
	api "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
	api "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"		
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
mainClassName = "io.opencaesar.oml.dsl.ide.diagram.launch.OmlServerLauncher"

shadowJar {
	from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
	configurations = [project.configurations.runtimeClasspath]
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA','schema/*',
		'.options', '.api_description', '*.profile', '*.html', 'about.*', 'about_files/*',
		'plugin.xml', 'systembundle.properties', 'profile.list')
	archiveBaseName = 'oml-server'
	classifier = null
	append('plugin.properties')
	mergeServiceFiles()
}

shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'

task copySrcGen(type: Copy) {
    from '../io.opencaesar.oml.dsl.ide/src-gen'
	into 'src-gen'
} 

compileJava.dependsOn 'copySrcGen'
processResources.dependsOn 'copySrcGen'

clean {
	delete 'src-gen'
}
